apply plugin: 'de.undercouch.download'

def modelMovinetA0Url = "https://www.kaggle.com/api/v1/models/google/movinet/tfLite/a0-stream-kinetics-600-classification-tflite-int8/1/download"
def modelMovinetA0File = "movinet_a0_stream_int8.tflite"

def modelMovinetA1Url = "https://www.kaggle.com/api/v1/models/google/movinet/tfLite/a1-stream-kinetics-600-classification-tflite-int8/1/download"
def modelMovinetA1File = "movinet_a1_stream_int8.tflite"

def modelMovinetA2Url = "https://www.kaggle.com/api/v1/models/google/movinet/tfLite/a2-stream-kinetics-600-classification-tflite-int8/1/download"
def modelMovinetA2File = "movinet_a2_stream_int8.tflite"

def downloadAndExtract(srcUrl, tarFile, extractedFile, finalDest) {
    // Download the tar.gz file
    download {
        src srcUrl
        dest tarFile
        overwrite false
    }

    // Extract the tar.gz file
    copy {
        from tarTree(resources.gzip(tarFile))
        into project.ext.ASSET_DIR
    }

    // Rename the extracted file
    copy {
        from file("${project.ext.ASSET_DIR}/${extractedFile}")
        into file(finalDest.parentFile)
        rename { String fileName ->
            finalDest.name
        }
    }

    // Clean up the tar.gz file and the extracted directory/file
    delete tarFile
    delete file("${project.ext.ASSET_DIR}/${extractedFile}")
}

task downloadMovinetA0 {
    doLast {
        downloadAndExtract(
                modelMovinetA0Url,
                file("${project.ext.ASSET_DIR}/movinet_a0_stream_int8.tar.gz"),
                '1.tflite',
                file("${project.ext.ASSET_DIR}/${modelMovinetA0File}")
        )
    }
}

task downloadMovinetA1 {
    doLast {
        downloadAndExtract(
                modelMovinetA1Url,
                file("${project.ext.ASSET_DIR}/movinet_a1_stream_int8.tar.gz"),
                '1.tflite',
                file("${project.ext.ASSET_DIR}/${modelMovinetA1File}")
        )
    }
}

task downloadMovinetA2 {
    doLast {
        downloadAndExtract(
                modelMovinetA2Url,
                file("${project.ext.ASSET_DIR}/movinet_a2_stream_int8.tar.gz"),
                '1.tflite',
                file("${project.ext.ASSET_DIR}/${modelMovinetA2File}")
        )
    }
}

preBuild.dependsOn downloadMovinetA0
preBuild.dependsOn downloadMovinetA1
preBuild.dependsOn downloadMovinetA2
