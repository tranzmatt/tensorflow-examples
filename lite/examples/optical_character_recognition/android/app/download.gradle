apply plugin: 'de.undercouch.download'

def downloadAndExtract(srcUrl, tarFile, extractedFile, finalDest) {
    // Download the tar.gz file
    download {
        src srcUrl
        dest tarFile
        overwrite false
    }

    // Extract the tar.gz file
    copy {
        from tarTree(resources.gzip(tarFile))
        into project.ext.ASSET_DIR
    }

    // Rename the extracted file
    copy {
        from file("${project.ext.ASSET_DIR}/${extractedFile}")
        into file(finalDest.parentFile)
        rename { String fileName ->
            finalDest.name
        }
    }

    // Clean up the tar.gz file and the extracted directory/file
    delete tarFile
    delete file("${project.ext.ASSET_DIR}/${extractedFile}")
}

task downloadTextDetectionModelFile {
    doLast {
        downloadAndExtract(
                'https://www.kaggle.com/api/v1/models/spsayakpaul/east-text-detector/tfLite/fp16/1/download',
                file("${project.ext.ASSET_DIR}/text_detection.tar.gz"),
                '1.tflite',
                file("${project.ext.ASSET_DIR}/text_detection.tflite")
        )
    }
}

task downloadTextRecognitionModelFile {
    doLast {
        downloadAndExtract(
                'https://www.kaggle.com/api/v1/models/kaggle/keras-ocr/tfLite/float16/2/download',
                file("${project.ext.ASSET_DIR}/text_recognition.tar.gz"),
                '2.tflite',
                file("${project.ext.ASSET_DIR}/text_recognition.tflite")
        )
    }
}

preBuild.dependsOn downloadTextDetectionModelFile, downloadTextRecognitionModelFile
